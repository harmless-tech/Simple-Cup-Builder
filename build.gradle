plugins {
    id "java"
    id "idea"
    id "application"

    id "com.github.gmazzo.buildconfig" version "2.0.2"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "com.github.spotbugs" version "4.6.0"
}

group "tech.harmless.simplecupbuilder"
version "0.0.1-ALPHA"

sourceCompatibility = 15
targetCompatibility = 15

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // All
    implementation group: "com.github.jezza", name: "toml", version: "1.2"
    implementation group: "com.google.code.gson", name: "gson", version: "2.8.6"

    // JGit was acting weird and it does not have module support.
    //implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.9.0.202009080501-r'

    // Annotations
    compileOnly group: "org.projectlombok", name: "lombok", version: "1.18.16"
    annotationProcessor group: "org.projectlombok", name: "lombok", version: "1.18.16"

    compileOnly group: "org.jetbrains", name: "annotations", version: "20.1.0"
    annotationProcessor group: "org.jetbrains", name: "annotations", version: "20.1.0"
    
    compileOnly group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.2.0"
    annotationProcessor group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.2.0"

    // Bugs
    spotbugs group: "com.github.spotbugs", name: "spotbugs", version: "4.2.0"
    // This plugin errors and complains about missing classes.
    //spotbugsPlugins group: "com.h3xstream.findsecbugs", name: "findsecbugs-plugin", version: "1.11.0"

    // Tests
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: "5.7.0"
    testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.7.0"
}

plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
    }
}

buildConfig {
    className("BuildConfig")
    packageName("tech.harmless.simplecupbuilder")

    buildConfigField("String", "NAME", "\"${rootProject.name}\"")
    buildConfigField("String", "VERSION", "\"${version}\"")
    buildConfigField("String", "AUTHOR_NAME", "\"Harmless_Tech\"")
    buildConfigField("String", "BUILD_TIME", "\"${new Date()}\"")

    sourceSets.getByName("test") {
        className("BuildConfig")
        packageName("tests")

        buildConfigField("String", "TEST", "\"Test\"")
    }
}

mainClassName = "tech.harmless.simplecupbuilder.SimpleCupBuilder"

task copyDeps(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs"
}

jar {
    dependsOn "copyDeps"

    manifest {
        attributes(
                "Implementation-Title": "Gradle",
                "Implementation-Version": archiveVersion,
                "Main-Class": "tech.harmless.simplecupbuilder.SimpleCupBuilder",
                "Class-Path": configurations.compileClasspath.collect { it.getName() }.join(" ")
        )
    }
}

application {
    mainModule = "tech.harmless.simplecupbuilder"
    mainClass = "tech.harmless.simplecupbuilder.SimpleCupBuilder"
    applicationDefaultJvmArgs = ["-Dfile.encoding=utf-8", "-Xmx2048m"]
}

task debug {
    group = "application"
    description = "Runs the program with assertions and custom debug args."

    run {
        args = ["--debug"]
    }

    doLast {
        application.applicationDefaultJvmArgs.asList().add("-ea")
    }

    finalizedBy "run"
}

spotbugs {
    showProgress = true
    onlyAnalyze = ["tech.harmless.simplecupbuilder.*"]
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = "fancy-hist.xsl"
        }
    }
}

test {
    useJUnitPlatform()
}
